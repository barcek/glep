#!/usr/bin/awk -f

# glep v 1.7.1
# Â©2023 barcek
# License: MIT
# @ github.com
# /barcek/glep

# Run three times per initial command,
# each run being a single named stage,
# all but no. 3 then running the next:
# 1. 'list' (BEGIN; runs no. 2)
# 2. 'grep' (BEGIN & LOOPS; runs no. 3)
# 3. 'echo' (LOOPS)
#
# Arguments to no. 1 are search terms,
# used by default to filter 'git log';
# any may be a prefixed root, author
# or commit object name for 'git show'

BEGIN { # incl. entire list stage

  # set default values
  # - general
  dir_root = root ? root : "../"
  # - special term formats
  arg_pre_base = "="
  #   - root directory
  arg_root_pre = arg_pre_base ">"
  #   - commit object name
  arg_name_pre = arg_pre_base "#"
  arg_name_len = length(arg_name_pre) + 7 # commit object name ([0-9a-f]{7})
  #   - author
  arg_auth_pre = arg_pre_base "@"

  stage = stage ? stage : "list"

  if("list" == stage) {

    # handle non-provision of search terms
    if(ARGC < 2) { print "No search term provided" > "/dev/stderr"; exit 1 }

    # extract specially formatted search terms, consolidating remaining
    for(i = 1; i < length(ARGV); i++) {
      # consolidate if not specially formatted
      split(ARGV[i], base_arr, arg_pre_base)
      if(length(base_arr[2]) != length(ARGV[i]) - length(arg_pre_base)) {
        terms_arr[j++] = ARGV[i]
        continue
      }
      # extract if root directory
      term_root = term_extract(ARGV[i], arg_root_pre)
      if(term_root) {
        split(term_root, path_arr, "/")
        dir_root = !length(path_arr[length(path_arr)]) ? term_root : term_root "/"
        continue
      }
      # extract if commit object name
      if(arg_name_len == length(ARGV[i])) {
        term_name = term_extract(ARGV[i], arg_name_pre)
        if(term_name) {
          name_str = term_name
          continue
        }
      }
      # extract if author
      term_auth = term_extract(ARGV[i], arg_auth_pre)
      if(term_auth) {
        auth_str = term_auth
        continue
      }
      # else consolidate
      terms_arr[j++] = ARGV[i]
    }
    # comma-separate any terms beyond name
    terms_str = "\"" terms_arr[0] "\""
    for(i = 1; i < length(terms_arr); i++) {
      terms_str = terms_str ",\"" terms_arr[i] "\""
    }
    # pipe list of dirnames to grep stage
    system(\
      "ls -F " dir_root " | glep -v stage=grep"\
                               " -v terms=" terms_str\
                               " -v  root="  dir_root\
                               " -v  name="  name_str\
                               " -v  auth="  auth_str\
    )
    exit 0
  }

  if("grep" == stage) {
    # build grep string for terms passed
    cmd_grep_col = !name ? "always" : "never"
    split(terms, terms_arr, ",")
    for(i = 1; i <= length(terms_arr); i++) {
      cmd_grep = cmd_grep " | grep --color=" cmd_grep_col " \"" terms_arr[i] "\""
    }
    # run commands for root directory
    grep_perform(dir_root)
  }
}

# LOOPS, for grep and echo stages

"grep" == stage && $0 ~ /\/$/ {
  # run commands for next subdirectory
  dir_next = dir_root $0
  grep_perform(dir_next)
}

"echo" == stage {
  # format and output matches for terms
  print "\033[1m" path "\033[0m" $0
}

# UTILS

# return term unprefixed if special else empty string
function term_extract(arg, pre) {
  split(arg, arr, pre)
  return length(arr[2]) == length(arg) - length(pre) ? arr[2] : ""
}

# build and run pipeline performing grep stage tasks
function grep_perform(dir_dest) {
  # set command to find git directory
  cmd_find = "cd " dir_dest " && ls -a | grep -E '^.git$' > /dev/null"
  # set command for full pipeline per commit object name
  cmd_full = !name ? cmd_find "&& matches=$("\
                                            "git log --branches=*"\
                                                   " --author=" auth\
                                                   " --oneline"\
                                                   " --format=\"%S | %as %an | %h %s\"" cmd_grep\
                                          ") "\
                              "&& cd - > /dev/null "\
                              "&& echo \"$matches\" | glep -v stage=echo"\
                                                         " -v path=" dir_dest\
                   : cmd_find "&& git show --color=always " name " 2> /dev/null " cmd_grep
  system(cmd_full)
}
